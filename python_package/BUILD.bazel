load("@pip//:requirements.bzl", "requirement")
load("@rules_proto_grpc//:defs.bzl", "proto_plugin")
load("@rules_proto_grpc_python//:defs.bzl", "python_grpc_compile")
load("@rules_python//python:packaging.bzl", "py_wheel")
load("@rules_python//python/entry_points:py_console_script_binary.bzl", "py_console_script_binary")
load(":defs.bzl", "proto_init_files")

# __init__.py and py.typed for proto package hierarchy

proto_init_files(
    name = "package_init",
    proto_srcs = "//:proto_srcs",
)

# mypy-protobuf plugin for _pb2_grpc.pyi stubs

py_console_script_binary(
    name = "mypy_grpc_plugin_bin",
    pkg = requirement("mypy-protobuf"),
    script = "protoc-gen-mypy_grpc",
)

proto_plugin(
    name = "mypy_grpc_plugin",
    outputs = ["{protopath|python}_pb2_grpc.pyi"],
    protoc_plugin_name = "mypy_grpc",
    tool = ":mypy_grpc_plugin_bin",
)

# Compile protos to Python + stubs

python_grpc_compile(
    name = "grpc_compiled",
    protos = ["//:all_protos"],
    extra_plugins = [
        "@rules_proto_grpc_python//:pyi_plugin",
        ":mypy_grpc_plugin",
    ],
)

# Package into wheel

filegroup(
    name = "grpc_package_files",
    srcs = [
        ":grpc_compiled",
        ":package_init",
    ],
)

py_wheel(
    name = "grpc_services_wheel",
    distribution = "demo",
    version = "0.1.0",
    deps = [":grpc_package_files"],
    strip_path_prefixes = [
        "python_package/grpc_compiled/",
        "python_package/",
    ],
    requires = [
        "grpcio>=1.60.0",
        "protobuf>=4.25.0",
    ],
    python_tag = "py3",
    visibility = ["//visibility:public"],
)
